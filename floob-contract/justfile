juno_container := 'juno_node_1' 
docker_prefix := 'docker exec -i juno_node_1'
# Note this file is local
code_id := `cat temp/code_id.txt 2> /dev/null || echo "no code id"`
contract_addr := `cat temp/contract_address.txt 2> /dev/null || echo "no contract address"`

set dotenv-load 

chain_id := `echo $CHAIN_ID`
denom := `echo $DENOM`
key_name := `echo $KEY_NAME`
# for testnet deployments
node_flag := `
if [ -z "${NODE+x}" ]; then
    echo ""
else
    echo "--node $NODE"
fi
`

deploy-local: restart-juno 
    {{docker_prefix}} apk add --no-cache jq
    just compile-wasm 
    docker cp artifacts/floob_contract.wasm {{juno_container}}:/floob_contract.wasm

upload-contract:
    docker exec -i {{juno_container}} \
        junod tx wasm store "/floob_contract.wasm" \
        --gas-prices 0.1{{denom}} --gas auto --gas-adjustment 1.3 \
        -y -b block \
        --chain-id {{chain_id}} \
        --from {{key_name}} --output json \
        | jq -r '.logs[0].events[] | select(.type == "store_code").attributes[] | select(.key == "code_id").value' \
        > temp/code_id.txt

instantiate-contract:
    {{docker_prefix}} junod tx wasm instantiate {{code_id}} \
        '{ "admin": "'"$(docker exec -i juno_node_1 junod keys show validator -a)"'" }' \
        --label "some contract" \
        --admin $(docker exec -i juno_node_1 junod keys show {{key_name}} -a) \
        --from {{key_name}} \
        --gas-prices 0.1ujunox \
        --gas auto \
        --gas-adjustment 1.3 \
        -y \
        -b block \
        --chain-id {{chain_id}} \
        --output json \
        | jq -r '.logs[0].events[] | select(.type == "instantiate").attributes[] | select(.key == "_contract_address").value' \
        > temp/contract_address.txt

populate-testing:
    {{docker_prefix}} junod tx wasm execute {{contract_addr}} \
        '{"create_thread": { "title": "Hello Floob", "description": "How Floob started", "content": ["Uh oh", "This works?"] }}' \
        --from {{key_name}} \
        --gas-prices 0.1{{denom}} \
        --gas auto \
        --gas-adjustment 1.3 \
        -y \
        -b block \
        --chain-id {{chain_id}} \
        --output json
 
restart-juno: 
    docker kill {{juno_container}} || true 
    docker rm {{juno_container}} || true 
    docker run --rm -d \
        --name {{juno_container}} \
        -p 1317:1317 \
        -p 26656:26656 \
        -p 26657:26657 \
        -e STAKE_TOKEN=ujunox \
        -e UNSAFE_CORS=true \
        ghcr.io/cosmoscontracts/juno:v11.0.0 \
        ./setup_and_run.sh juno16g2rahf5846rxzp3fwlswy08fz8ccuwk03k57y
 
compile-wasm:
    docker run --rm -v "$(pwd)":/code \
        --mount type=volume,source="$(basename "$(pwd)")_cache",target=/code/target \
        --mount type=volume,source=registry_cache,target=/usr/local/cargo/registry \
        cosmwasm/rust-optimizer:0.12.8

deploy-local-m1: restart-juno compile-wasm-m1
    docker cp artifacts/floob_contract-aarch64.wasm {{juno_container}}:/floob_contract.wasm
    just upload-contract

compile-wasm-m1:
    docker run --rm -v "$(pwd)":/code \
        --mount type=volume,source="$(basename "$(pwd)")_cache",target=/code/target \
        --mount type=volume,source=registry_cache,target=/usr/local/cargo/registry \
        cosmwasm/rust-optimizer-arm64:0.12.12

# Testnet messing around
query_msgs:
    junod query wasm contract-state smart \
        juno1vzm5qzqwhvazedkt3z9y6j73z68scwr6kwjf8wjfr6drpf9wcxzqnrjf8p \
        '{ "get_thread": { "id": 0 } }' \
        {{node_flag}} \
        --output json 