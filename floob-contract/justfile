juno_container := 'juno_node_1' 
docker_prefix := 'docker exec -i juno_node_1'
# Note this file is local
code_id := `cat temp/code_id.txt 2> /dev/null || echo "no code id"`
contract_addr := `cat temp/contract_address.txt 2> /dev/null || echo "no contract address"`

deploy-local: restart-juno 
    {{docker_prefix}} apk add --no-cache jq
    just compile-wasm 
    docker cp artifacts/floob_contract.wasm {{juno_container}}:/floob_contract.wasm
    just upload-contract

upload-contract:
    docker exec -i {{juno_container}} \
        junod tx wasm store "/floob_contract.wasm" \
        --gas-prices 0.1ujunox --gas auto --gas-adjustment 1.3 \
        -y -b block --chain-id testing \
        --from validator --output json \
        | jq '.logs[0].events[] | select(.type == "store_code").attributes[] | select(.key == "code_id").value' \
        > temp/code_id.txt

instantiate-contract:
    {{docker_prefix}} junod tx wasm instantiate {{code_id}} \
        '{ "admin": "'"$(docker exec -i juno_node_1 junod keys show validator -a)"'" }' \
        --label "some contract" \
        --admin $(docker exec -i juno_node_1 junod keys show validator -a) \
        --from validator \
        --gas-prices 0.1ujunox \
        --gas auto \
        --gas-adjustment 1.3 \
        -y \
        -b block \
        --chain-id testing \
        --output json \
        | jq '.logs[0].events[] | select(.type == "instantiate").attributes[] | select(.key == "_contract_address").value' \
        > temp/contract_address.txt

populate-testing:
    {{docker_prefix}} junod tx wasm execute {{contract_addr}} \
        '{"create_thread": { "title": "Hello Floob", "description": "How Floob started", "content": ["Uh oh", "This works?"] }}' \
        --from validator \
        --gas-prices 0.1ujunox \
        --gas auto \
        --gas-adjustment 1.3 \
        -y \
        -b block \
        --chain-id testing \
        --output json
 
restart-juno: 
    docker kill {{juno_container}} || true 
    docker rm {{juno_container}} || true 
    docker run --rm -d \
        --name {{juno_container}} \
        -p 1317:1317 \
        -p 26656:26656 \
        -p 26657:26657 \
        -e STAKE_TOKEN=ujunox \
        -e UNSAFE_CORS=true \
        ghcr.io/cosmoscontracts/juno:v11.0.0 \
        ./setup_and_run.sh juno16g2rahf5846rxzp3fwlswy08fz8ccuwk03k57y
 
compile-wasm:
    docker run --rm -v "$(pwd)":/code \
        --mount type=volume,source="$(basename "$(pwd)")_cache",target=/code/target \
        --mount type=volume,source=registry_cache,target=/usr/local/cargo/registry \
        cosmwasm/rust-optimizer:0.12.8

deploy-local-m1: restart-juno compile-wasm-m1
    docker cp artifacts/floob_contract-aarch64.wasm {{juno_container}}:/floob_contract.wasm
    just upload-contract

compile-wasm-m1:
    docker run --rm -v "$(pwd)":/code \
        --mount type=volume,source="$(basename "$(pwd)")_cache",target=/code/target \
        --mount type=volume,source=registry_cache,target=/usr/local/cargo/registry \
        cosmwasm/rust-optimizer-arm64:0.12.12